Alphabet

  A B C D E F G H I J K L M N Ñ O P Q R S T U V W X Y Z %+:0
  a b c d e f g h i j k l m n ñ o p q r s t u v w x y z %+:0

 %{R%}

 %{E%}

 %{um%}:0 ! For to trigger the -um- infix

 %{in%}:0 ! For to trigger the -in- infix

;

Sets

  Cns = b c d f g h j k l m n ñ p q r s t  v w x y z ;
  Vow = a e i o u ;

;

Rules

"R realization as Consonant"
%{R%}:CC <=> _ ( 0:? 0:? ) %{E%}: CC ; where CC in Cns ;

"R deletion"
%{R%}:0 <=> _ ( 0:? 0:? ) %{E%}: VV ; where VV in Vow ;

"E realization as Vowel"
%{E%}:VV <=> ( 0:u 0:m ) _ (C:) VV ; where VV in Vow ;

"U insertion in -um- infix"
0:u <=> .#. ?: _ (0:m) \0:* %{um%}:0 ;
!This Rule and the following rules may require a change of contexts
! essentially, it says that there are no prefixes before an infix, which is not true for Cebuano
! See how this is in Tagalog and Tagabawa. Jaska 2024-07-04

"M insertion in -um- infix"
0:m <=> .#. ?: 0:u _ \0:* %{um%}:0 ;

"I insertion in -in- infix"
0:i <=> .#. ?: _ (0:n) \0:* %{in%}:0 ;

"N insertion in -in- infix"
0:n <=> .#. ?: 0:i _ \0:* %{in%}:0 ;

;

